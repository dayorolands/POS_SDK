apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion versions.compileSdk
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildFeatures {
        dataBinding true
        compose true
    }
    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
        releaseStaging {
            matchingFallbacks = ['release', 'debug']
        }
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation project(path: ':core')
    implementation project(path: ':poscore')
    implementation project(path: ':uicentral')

    api("org.jpos:jpos:2.1.4") {
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.sleepycat', module: 'je'
    }

    implementation 'com.loopj.android:android-async-http:1.4.11'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.serialization}"
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "androidx.core:core-ktx:${versions.coreKtx}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation "androidx.room:room-runtime:${versions.room}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    kapt "androidx.room:room-compiler:${versions.room}"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:${versions.room}"
    testImplementation 'org.json:json:20210307'

    // Test helpers
    testImplementation "androidx.room:room-testing:${versions.room}"
    androidTestImplementation "androidx.room:room-testing:${versions.room}"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-common-java8:${versions.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}"

    testImplementation 'junit:junit:4.13.2'
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${versions.desugarJdk}"
}
